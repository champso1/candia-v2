set terminal pdf enhanced color notransparent

set border lw 2
set size ratio 0.65

set output "ratio.pdf"
set ylabel "N3LO/NNLO" offset 2,0
set xlabel "x" offset 0,0.7

x0 = 0.1
f(x) = \
    (x < 1e-4) ? (log10(x) + 5) : \
    (x < 1e-3) ? (log10(x) + 4) + 1 : \
    (x < 1e-2) ? (log10(x) + 3) + 2 : \
    (x < 1e-1) ? (log10(x) + 2) + 3 : \
    (x < 0.5)  ? 4 + (x - 0.1)/(0.4) : \
                 5 + (x - 0.5)/(0.5)

# set logscale x
set yrange [0.94:1.08]
set xrange [f(1e-5):f(1.0)]

set xtics (\
    "10@^{-5}" f(1e-5),\
    "10@^{-4}" f(1e-4),\
    "10@^{-3}" f(1e-3),\
    "10@^{-2}" f(1e-2),\
    "10@^{-1}" f(1e-1),\
    "0.5"      f(0.5),\
    "1.0"      f(1.0)\
)
set mxtics 5
set grid


array names[11]
names[1] = "u"
names[2] = "d"
names[3] = "s"
names[4] = "c"
names[5] = "b"

names[7] = "ub"
names[8] = "db"
names[9] = "sb"
names[10] = "cb"
names[11] = "bb"

plot for [i=3:7] 'ratio.dat' using (f($1)):i with lines lw 2 title names[i-2],\
for [i=9:13] '' using (f($1)):i with lines lw 2 title names[i-2]


